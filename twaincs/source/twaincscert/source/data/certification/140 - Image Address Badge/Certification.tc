;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Image Address Badge
;   Check if Image Addressing is supported by the driver.  This is a badge, failure does not
;   invalidate scanner certification.  Instead, passage adds Image Address support to the cert.
;
;   The following are required:
;     CAP_IAFIELDA_LEVEL, CAP_IAFIELDB_LEVEL, CAP_IAFIELDC_LEVEL, CAP_IAFIELDD_LEVEL
;     CAP_IAFIELDA_PRINTFORMAT, CAP_IAFIELDB_PRINTFORMAT, CAP_IAFIELDC_PRINTFORMAT, CAP_IAFIELDD_PRINTFORMAT
;     CAP_IAFIELDA_VALUE, CAP_IAFIELDB_VALUE, CAP_IAFIELDC_VALUE, CAP_IAFIELDD_VALUE
;     TWEI_IALEVEL (values TWIA_FIXED, TWIA_LEVEL1, TWIA_LEVEL2, TWIA_LEVEL3)
;     TWEI_IAFIELDA_VALUE, TWEI_IAFIELDA_VALUE, TWEI_IAFIELDA_VALUE, TWEI_IAFIELDA_VALUE
;
;   If the scanner supports printing, the following are required:
;     CAP_PRINTERMODE (value TWPM_IMAGEADDRESSSTRING)
;     TWEI_PRINTERTEXT
;
;   The following are optional, and only tested if present in the driver:
;     CAP_IAFIELDE_LEVEL
;     CAP_IAFIELDE_PRINTFORMAT
;     CAP_IAFIELDE_VALUE
;     TWEI_IALEVEL (value TWIA_LEVEL4)
;     TWEI_IAFIELDE_VALUE
;
; Arguments
;   arg:1 - TW_IDENTITY.ProductName of scanner to test
;   arg:2 - global to set on fail
;
; Globals
;   g_localcertifyresult - global to report to caller on fail
;
; Locals
;   twidentity.productname - product name of scanner to test
;   globaltosetonfail - name says it all
;
; Returns
;   (none)



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Initialize stuff, load tables if not already done for us...
;
echo.titlesuite 'Image Address Badge v1.0 01-Sep-2021'
echo.yellow '*************************************************************************************'
echo.yellow '*** This tests the Image Address Badge.  Skipping or failing means the driver     ***'
echo.yellow '*** does not support this functionality.  It does not prevent certification.      ***'
echo.yellow '*************************************************************************************'
;
setlocal twidentity.productname '${arg:1}'
setlocal globaltosetonfail '${arg:2}'
setglobal g_localcertifyresult 'pass' ; assume success
;
if '${get:TW_ARRAY.ItemType}' != '' goto MAIN.LOADTABLES.SKIP
run '${folder:certification}/LoadTables'
:MAIN.LOADTABLES.SKIP



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Open the scanner driver, run the tests, close the scanner driver...
;
call CERTIFY '${folder:certification}/Opends'  '${get:twidentity.productname}' 'g_localcertifyresult' 'embedded'
if '${get:g_localcertifyresult}' != 'pass' goto MAIN.CLOSEDS
;
call CERTIFY 'Image Address Badge'             '${get:twidentity.productname}' 'g_localcertifyresult'
;
:MAIN.CLOSEDS
call CERTIFY '${folder:certification}/Closeds' '${get:twidentity.productname}' 'g_localcertifyresult' 'embedded'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Summary and cleanup...
;
; Did we pass?
if '${get:g_localcertifyresult}' == 'skip' goto MAIN.SKIP
if '${get:g_localcertifyresult}' != 'pass' goto MAIN.ERROR
echo ''
echo.passfail 'BADGE SUMMARY' 'pass'
return 'pass'
;
; Unsupported...
:MAIN.SKIP
echo ''
echo.passfail 'BADGE SUMMARY' 'skip'
setglobal '${get:globaltosetonfail}' 'skip'
return 'skip'
;
; Ruh-roh...
:MAIN.ERROR
echo ''
echo.passfail 'BADGE SUMMARY' 'fail'
setglobal '${get:globaltosetonfail}' 'fail'
return 'fail'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Function: Certify
; arg:1 - script to run
; arg:2 - TW_IDENTITY.ProductName of scanner to test
; arg:3 - global value to update on fail
; arg:4 - embeded flag for open/close, or folder for DAT_IMAGE*XFER calls
; returns: (none)
;
:CERTIFY
;
; If a failure has occurred on a previous test, skip the rest of the tests,
; except for closing, that would be bad...
if '${arg:1}' ~contains 'Closeds' goto CERTIFY.RUN
if '${get:${arg:3}}' == 'skip' goto CERTIFY.SKIP
if '${get:${arg:3}}' == 'fail' goto CERTIFY.FAIL
;
; Run the test...
:CERTIFY.RUN
run '${arg:1}' '${arg:2}' '${arg:4}'
if '${ret:}' == 'skip' goto CERTIFY.SKIP
if '${ret:}' != 'pass' goto CERTIFY.FAIL
return 'pass'
;
; Handle skipping, set the global so we have a record...
:CERTIFY.SKIP
setglobal '${arg:3}' 'skip'
return 'skip'
;
; Handle failure, set the global so we have a record...
:CERTIFY.FAIL
setglobal '${arg:3}' 'fail'
return 'fail'
