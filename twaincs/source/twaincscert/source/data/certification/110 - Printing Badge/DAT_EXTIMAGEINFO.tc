;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; DAT_EXTIMAGEINFO
;   Check the return values from DAT_EXTIMAGEINFO to make sure the printer
;   metadata is being properly reported.
;
;   Here's the basic flow:
;       DAT_EXTIMAGEINFO
;       confirm that we have the following TWEI_PRINTER* items
;           TWEI_PRINTERTEXT
;           TWEI_PRINTER
;
; Arguments
;   arg:1 - indent the passfail text
;   arg:2 - base filename (no extension)
;   arg:3 - expected value for TWEI_PRINTER
;
; Locals
;   printerdata - the return value from DAT_EXTIMAGEINFO
;   name - current test being done
;   indent - indent the passfail text
;   twinfo.infoid.index - index to TW_INFO[n].infoid from the start of barcodedata
;   twinfo.itemtype.index - index to TW_INFO[n].itemtype from the start of barcodedata
;   twinfo.numitems.index - index to TW_INFO[n].numitems from the start of barcodedata
;   twinfo.returncode.index - index to TW_INFO[n].returncode from the start of barcodedata
;   twinfo.item.index - index to TW_INFO[n].item from the start of barcodedata
;
; Returns
;   pass or fail
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Get the extended image info, save the data to 'printerdata'.
;
setlocal indent '${arg:1}    '
setlocal name 'dg_image/dat_extimageinfo/msg_get'
dsmentry src ds dg_image dat_extimageinfo msg_get '2,TWEI_PRINTER,0,0,0,0,TWEI_PRINTERTEXT,0,0,0,0'
if '${sts:}' != 'SUCCESS' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
setlocal printerdata '${ret:}'
filewrite '${arg:2}_extimageinfo.txt' '${get:printerdata}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Confirm that we got back all the printer extimageinfo data...
;
setlocal name 'Found TWEI_PRINTER'
if '${get:printerdata}' !contains ',TWEI_PRINTER,' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
setlocal name 'Found TWEI_PRINTERTEXT'
if '${get:printerdata}' !contains ',TWEI_PRINTERTEXT,' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Init our TWINFO indecies for TW_INFO[0]...
;
setlocal twinfo.infoid.index '${get:TW_INFO.InfoID}'
setlocal twinfo.itemtype.index '${get:TW_INFO.ItemType}'
setlocal twinfo.numitems.index '${get:TW_INFO.NumItems}'
setlocal twinfo.returncode.index '${get:TW_INFO.ReturnCode}'
setlocal twinfo.item.index '${get:TW_INFO.Item}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Validate the contents of TWEI_PRINTER, which is in TW_INFO[0]...
;
setlocal name 'TW_INFO[0].InfoID is TWEI_PRINTER'
if '${getindex:printerdata:${get:twinfo.infoid.index}}' != 'TWEI_PRINTER' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
;
setlocal name 'TW_INFO[0].ItemType is TWTY_UINT16'
if '${getindex:printerdata:${get:twinfo.itemtype.index}}' != 'TWTY_UINT16' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
;
setlocal name 'TW_INFO[0].NumItems is 1'
if '${getindex:printerdata:${get:twinfo.numitems.index}}' != '1' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
;
setlocal name 'TW_INFO[0].ReturnCode is TWRC_SUCCESS'
if '${getindex:printerdata:${get:twinfo.returncode.index}}' != 'TWRC_SUCCESS' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
;
setlocal name 'TW_INFO[0].Item is ${arg:3}'
if '${gettwei:TWEI_PRINTER:${getindex:printerdata:${get:twinfo.item.index}}}' != '${arg:3}' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Increment our TWINFO indecies for TW_INFO[1]...
;
increment twinfo.infoid.index '${get:twinfo.infoid.index}' 5 ; skip to the InfoID in the next TW_INFO
increment twinfo.itemtype.index '${get:twinfo.itemtype.index}' 5 ; skip to the InfoID in the next TW_INFO
increment twinfo.numitems.index '${get:twinfo.numitems.index}' 5 ; skip to the NumItems in the next TW_INFO
increment twinfo.returncode.index '${get:twinfo.returncode.index}' 5 ; skip to the ReturnCode in the next TW_INFO
increment twinfo.item.index '${get:twinfo.item.index}' 5 ; skip to the ReturnCode in the next TW_INFO



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Validate the contents of TWEI_PRINTERTEXT, which is in TW_INFO[1]...
;
setlocal name 'TW_INFO[1].InfoID is TWEI_PRINTERTEXT'
if '${getindex:printerdata:${get:twinfo.infoid.index}}' != 'TWEI_PRINTERTEXT' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
;
setlocal name 'TW_INFO[1].ItemType is TWTY_STR255'
if '${getindex:printerdata:${get:twinfo.itemtype.index}}' != 'TWTY_STR255' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
;
setlocal name 'TW_INFO[1].NumItems is 1'
if '${getindex:printerdata:${get:twinfo.numitems.index}}' != '1' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
;
setlocal name 'TW_INFO[1].ReturnCode is TWRC_SUCCESS'
if '${getindex:printerdata:${get:twinfo.returncode.index}}' != 'TWRC_SUCCESS' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
;
; The printer text is behind a handle...
setlocal handle ${getindex:printerdata:${get:twinfo.item.index}}
setlocal printertext '${gethandle:${get:handle}:TWTY_STR255:0}'
;
setlocal name 'TW_INFO[1].Item contains "TWAIN" (${get:printertext})'
if '${get:printertext}' !contains 'TWAIN' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
;
setlocal name 'TW_INFO[1].Item contains "TEST" (${get:printertext})'
if '${get:printertext}' !contains 'TEST' goto MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'pass'
;
free handle item



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Summary and cleanup...
;
; Did we pass? (do an extra check)
:MAIN.PASS
return 'pass'
;
; Ruh-roh...
:MAIN.ERROR.ECHOPASSFAIL
echo.passfail '${get:indent}${get:name}' 'fail - ${sts:} ${ret:}'
return 'fail'
