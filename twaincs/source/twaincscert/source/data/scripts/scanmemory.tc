;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Scanfile
;   Scan with DAT_IMAGEMEMXFER...
;
; Arguments
;   arg:1 - TW_IDENTITY.ProductName of scanner to test
;
; Globals
;   (none)
;
; Locals
;   image.pointer - pointer to memory allocated for transfers
;   image.count - count of images, starting at 1
;   image.folder - location of the images
;   twainstate - pass/fail result of all tests
;
; Returns
;   pass or fail
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Init stuff...
;
run 'LoadTables'
setlocal image.pointer '0'
setlocal image.count '0'
setlocal image.folder '${folder:desktop}/twaincscert'
setlocal twainstate '0'
image cleanfolder '${get:image.folder}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Load and open the DSM...
;
setlocal twainstate '1'
dsmload
echo 'dsmload'
setlocal twainstate '2'
dsmentry src null dg_control dat_parent msg_opendsm hwnd
echo.progress 'parent/opendsm' '${sts:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
setlocal twainstate '3'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Open the driver...
;
dsmentry src null dg_control dat_identity msg_opends '0,0,0,ENGLISH_USA,USA,,2,4,0x0,,,${arg:1}'
setlocal twidentity '${ret:}'
echo.progress 'identity/opends' '${sts:} - ${get:twidentity}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
setlocal twainstate '4'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Set CAP_INDICATORS to TRUE...
;
dsmentry src ds dg_control dat_capability msg_set 'CAP_INDICATORS,TWON_ONEVALUE,TWTY_BOOL,TRUE'
echo.progress 'capability/set' '${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Set ICAP_XFERMECH to TWSX_MEMORY...
;
dsmentry src ds dg_control dat_capability msg_set 'ICAP_XFERMECH,TWON_ONEVALUE,TWTY_UINT16,TWSX_MEMORY'
echo.progress 'capability/set' '${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Set ICAP_PIXELTYPE to TWPT_RGB (if we can)
;
dsmentry src ds dg_control dat_capability msg_get 'ICAP_PIXELTYPE,0,0,0'
echo.progress 'capability/get' '${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
if '${ret:}' !~contains 'TWPT_RGB' goto MAIN.PIXELTYPE.SKIP
dsmentry src ds dg_control dat_capability msg_set 'ICAP_PIXELTYPE,TWON_ONEVALUE,TWTY_UINT16,TWPT_RGB'
echo.progress 'capability/set' '${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
:MAIN.PIXELTYPE.SKIP



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Set ICAP_COMPRESSION to TWCP_JPEG
;
dsmentry src ds dg_control dat_capability msg_set 'ICAP_COMPRESSION,TWON_ONEVALUE,TWTY_UINT16,TWCP_NONE'
echo.progress 'capability/set' '${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Get the preferred size for memory transfers, and allocate
; a pointer for the memory transfers...
;
dsmentry src ds dg_control dat_setupmemxfer msg_get '0,0,0'
echo.progress 'setupmemxfer/get' '${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
setlocal twsetupmemxfer.preferred '${ret:${get:TW_SETUPMEMXFER.Preferred}}'
if '${get:twsetupmemxfer.preferred}' <= '0' goto MAIN.FAIL
allocate pointer image.pointer '${get:twsetupmemxfer.preferred}'
echo.progress 'allocate pointer' '${get:image.pointer} size=${get:twsetupmemxfer.preferred}'
if '${get:image.pointer}' == '0' goto MAIN.FAIL



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Start scanning...
;
wait reset
dsmentry src ds dg_control dat_userinterface msg_enableds '0,0'
echo.progress 'userinterface/enableds' '${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
setlocal twainstate '5'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Wait for MSG_XFERREADY, if we don't see it then scoot...
;
wait 30000
echo.progress 'wait' '${ret:}'
if '${ret:}' !~contains 'msg_xferready' goto MAIN.PASS



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Do a memory transfer...
; This little song and dance with the states is so that we can
; start the transfer in state 6 with each new image, but switch
; to state 7 if we need multiple calls to capture the current
; image...
;
:MAIN.NEXTIMAGEMEMXFER.STATE6
setlocal twainstate '6'
increment image.count '${get:image.count}' 1
image free memimage
goto MAIN.NEXTIMAGEMEMXFER
:MAIN.NEXTIMAGEMEMXFER.STATE7
setlocal twainstate '7'
;
:MAIN.NEXTIMAGEMEMXFER
dsmentry src ds dg_image dat_imagememxfer msg_get '0,0,0,0,0,0,0,9,${get:twsetupmemxfer.preferred},${get:image.pointer}'
echo.progress 'imagememxfer/get' '${sts:} - ${ret:}'
if '${sts:}' == 'XFERDONE' goto MAIN.NEXTIMAGEMEMXFER.XFERDONE
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
image append memimage '${ret:}'
goto MAIN.NEXTIMAGEMEMXFER.STATE7
:MAIN.NEXTIMAGEMEMXFER.XFERDONE
image append memimage '${ret:}'
setlocal twainstate '7'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; We need image info to make sense of this data...
;
dsmentry src ds dg_image dat_imageinfo msg_get '0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0'
echo.progress 'imagememxfer/get' '${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
image addheader memimage '${ret:}'
image save memimage memory '${get:image.folder}/img${format:D6|${get:image.count}}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; End the transfer, see if we're done...
;
dsmentry src ds dg_control dat_pendingxfers msg_endxfer '0,0'
echo.progress 'pendingxfers/endxfer' '${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
if '${ret:${get:TW_PENDINGXFERS.Count}}' != '0' goto MAIN.NEXTIMAGEMEMXFER.STATE6
image free memimage
setlocal twainstate '5'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; We've passed...
;
:MAIN.PASS
setlocal main.result pass
goto MAIN.EXIT



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; We've run into a problem...
;
:MAIN.FAIL
echo.progress 'ERROR' '${sts:}'
image free memimage
setlocal main.result fail
goto MAIN.EXIT



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Rewind the driver, close and unload the DSM, return...
;
:MAIN.EXIT
free pointer image.pointer
call TWAIN.REWIND '${get:twainstate}' '${get:twidentity}'
return '${get:main.result}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Function: Rewind from any TWAIN state...
; arg:1 - twainstate
; arg:2 - twidentity
; returns: (nothing)
;
:TWAIN.REWIND
;
setlocal twainstate '${arg:1}'
if '${get:twainstate}' < '7' goto REWIND.6
dsmentry src ds dg_control dat_pendingxfers msg_endxfer '0,0'
echo.progress 'pendingxfers/endxfer' '${sts:}'
setlocal twainstate '6'
:REWIND.6
if '${get:twainstate}' < '6' goto REWIND.5
dsmentry src ds dg_control dat_pendingxfers msg_reset '0,0'
echo.progress 'pendingxfers/reset' '${sts:}'
setlocal twainstate '5'
:REWIND.5
if '${get:twainstate}' < '5' goto REWIND.4
dsmentry src ds dg_control dat_userinterface msg_disableds '0,0'
echo.progress 'userinterface/disableds' '${sts:}'
setlocal twainstate '4'
:REWIND.4
if '${get:twainstate}' < '4' goto REWIND.3
dsmentry src null dg_control dat_identity msg_closeds '${arg:2}'
echo.progress 'identity/closeds' '${sts:}'
setlocal twainstate '3'
:REWIND.3
if '${get:twainstate}' < '3' goto REWIND.2
dsmentry src null dg_control dat_parent msg_closedsm hwnd
echo.progress 'parent/closedsm' '${sts:}'
setlocal twainstate '2'
:REWIND.2
if '${get:twainstate}' < '2' goto REWIND.1
dsmunload
echo 'dsmunload'
setlocal twainstate '1'
:REWIND.1
return ${get:twainstate}'
