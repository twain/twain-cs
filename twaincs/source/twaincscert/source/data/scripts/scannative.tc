;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Scanfile
;   Scan with DAT_IMAGENATIVEXFER...
;
; Arguments
;   arg:1 - TW_IDENTITY.ProductName of scanner to test
;
; Globals
;   (none)
;
; Locals
;   image.handle - handle to native transfer
;   image.count - count of images, starting at 1
;   image.folder - location of the images
;   twainstate - pass/fail result of all tests
;
; Returns
;   pass or fail
;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Init stuff...
;
run 'LoadTables'
setlocal image.handle '0'
setlocal image.count '0'
setlocal image.folder '${folder:desktop}/twaincscert'
setlocal twainstate '0'
image cleanfolder '${get:image.folder}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Load and open the DSM...
;
setlocal twainstate '1'
dsmload
echo 'dsmload'
setlocal twainstate '2'
dsmentry src null dg_control dat_parent msg_opendsm hwnd
echo 'parent/opendsm.............${sts:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
setlocal twainstate '3'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Open the driver...
;
dsmentry src null dg_control dat_identity msg_opends '0,0,0,ENGLISH_USA,USA,,2,4,0x0,,,${arg:1}'
setlocal twidentity '${ret:}'
echo 'identity/opends............${sts:} - ${get:twidentity}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
setlocal twainstate '4'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Set ICAP_XFERMECH to TWSX_NATIVE...
;
dsmentry src null dg_control dat_capability msg_set 'ICAP_XFERMECH,TWON_ONEVALUE,TWTY_UINT16,TWSX_NATIVE'
echo 'capability/set.............${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Start scanning...
;
wait reset
dsmentry src null dg_control dat_userinterface msg_enableds '0,0'
echo 'userinterface/enableds.....${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
setlocal twainstate '5'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Wait for MSG_XFERREADY, if we don't see it then scoot...
;
wait 30000
echo 'wait.......................${ret:}'
if '${ret:}' !~contains 'msg_xferready' goto MAIN.PASS



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Do a native transfer...
;
:MAIN.NEXTIMAGENATIVEXFER
setlocal twainstate '6'
increment image.count '${get:image.count}' 1
dsmentry src ds dg_image dat_imagenativexfer msg_get '0'
setlocal image.handle '${ret:}'
echo 'imagenativexfer/get........${sts:} - ${ret:}'
if '${sts:}' != 'XFERDONE' goto MAIN.FAIL
image save image.handle native '${get:image.folder}/img${format:D6|${get:image.count}}'
free handle image.handle
setlocal twainstate '7'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; End the transfer, see if we're done...
;
dsmentry src ds dg_control dat_pendingxfers msg_endxfer '0,0'
echo 'pendingxfers/endxfer.......${sts:} - ${ret:}'
if '${sts:}' != 'SUCCESS' goto MAIN.FAIL
if '${ret:${get:TW_PENDINGXFERS.Count}}' != '0' goto MAIN.NEXTIMAGENATIVEXFER
setlocal twainstate '5'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; We've passed...
;
:MAIN.PASS
setlocal main.result pass
goto MAIN.EXIT



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; We've run into a problem...
;
:MAIN.FAIL
echo 'ERROR...........${sts:}'
setlocal main.result fail
goto MAIN.EXIT



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Rewind the driver, close and unload the DSM, return...
;
:MAIN.EXIT
free handle image.handle
call TWAIN.REWIND '${get:twainstate}' '${get:twidentity}'
return '${get:main.result}'



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Function: Rewind from any TWAIN state...
; arg:1 - twainstate
; arg:2 - twidentity
; returns: (nothing)
;
:TWAIN.REWIND
;
setlocal twainstate '${arg:1}'
if '${get:twainstate}' < '7' goto REWIND.6
dsmentry src ds dg_control dat_pendingxfers msg_endxfer '0,0'
echo 'pendingxfers/endxfer.......${sts:}'
setlocal twainstate '6'
:REWIND.6
if '${get:twainstate}' < '6' goto REWIND.5
dsmentry src ds dg_control dat_pendingxfers msg_reset '0,0'
echo 'pendingxfers/reset.........${sts:}'
setlocal twainstate '5'
:REWIND.5
if '${get:twainstate}' < '5' goto REWIND.4
dsmentry src ds dg_control dat_userinterface msg_disableds '0,0'
echo 'userinterface/disableds....${sts:}'
setlocal twainstate '4'
:REWIND.4
if '${get:twainstate}' < '4' goto REWIND.3
dsmentry src null dg_control dat_identity msg_closeds '${arg:2}'
echo 'identity/closeds...........${sts:}'
setlocal twainstate '3'
:REWIND.3
if '${get:twainstate}' < '3' goto REWIND.2
dsmentry src null dg_control dat_parent msg_closedsm hwnd
echo 'parent/closedsm............${sts:}'
setlocal twainstate '2'
:REWIND.2
if '${get:twainstate}' < '2' goto REWIND.1
dsmunload
echo 'dsmunload'
setlocal twainstate '1'
:REWIND.1
return ${get:twainstate}'
